plugins {
	id "checkstyle"
	id "io.spring.javaformat" version "$javaFormatVersion"
	id "java-gradle-plugin"
	id "maven-publish"
	id "io.spring.compatibility-test"
}

description = "Gradle Enterprise Conventions Gradle plugin"

repositories {
	mavenCentral()
	gradlePluginPortal()
}

configurations {
	compatibilityTestImplementation
}

dependencies {
	implementation(project(":gradle-enterprise-conventions-core"))
	compileOnly("com.gradle:gradle-enterprise-gradle-plugin:${gradleEnterprisePluginVersion}")
	testImplementation("com.gradle:gradle-enterprise-gradle-plugin:${gradleEnterprisePluginVersion}")
}

gradlePlugin {
	plugins {
		gradleEnterprisePlugin {
			displayName = project.description
			description = project.description
			id = 'io.spring.ge.conventions'
			implementationClass = 'io.spring.ge.conventions.gradle.GradleEnterpriseConventionsPlugin'
		}
	}
}

compatibilityTest {
	useJUnitPlatform()
	dependency('Gradle Enterprise Plugin') { gePlugin ->
		gePlugin.groupId = 'com.gradle'
		gePlugin.artifactId = 'gradle-enterprise-gradle-plugin'
		gePlugin.versions = [
			'3.1.1', '3.2.1', '3.3.4', '3.4.1'
		]
	}
}

tasks.withType(Test) { testTask ->
	if (testTask.name.startsWith("compatibilityTest_")) {
		testTask.filter {
			includeTestsMatching "*IntegrationTests"
		}
	}
}

